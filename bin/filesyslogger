#!/usr/bin/env perl
#
#This software is Copyright (c) 2021 by Zane C. Bowers-Hadley.
#
#This is free software, licensed under:
#
#  The Artistic License 2.0 (GPL Compatible)

use strict;
use warnings;
use TOML qw(from_toml);
use File::Syslogger;
use File::Slurp qw(read_file);
use Getopt::Long qw(:config pass_through);

my $version = '0.0.1';

my $help;
my $toml_file = '/usr/local/etc/filesyslogger.toml';
my $pri;
my $fac;
my $socket;
my $program;
GetOptions(
	'p=s'  => \$pri,
	'P=s'  => \$program,
	'f=s'  => \$fac,
	't=s'  => \$toml_file,
	's=s'  => \$socket,
	'h'    => \$help,
	'help' => \$help,
);

# make sure the file exists
if ( !-f $toml_file ) {
	die( '"' . $toml_file . '" does not exist' );
}

# read the in or die
my $toml_raw = read_file($toml_file) or die 'Failed to read "' . $toml_file . '"';

# read the specified config
my ( $toml, $err ) = from_toml($toml_raw);
unless ($toml) {
	die "Error parsing toml,'" . $toml_file . "'" . $err;
}

# read in the defaults, letting the switches over ride
if (
	defined( $toml->{program} )
	&&  !defined($program)  )
{
	$program = $toml->{program};
}
if (
	defined( $toml->{'facility'} )
	&&  !defined($fac)  )
{
	$fac = $toml->{facility};
}
if (
	defined( $toml->{priority} )
	&&  !defined($pri)  )
{
	$pri = $toml->{priority};
}
if (
	defined( $toml->{socket} )
	&&  !defined($socket)  )
{
	$socket = $toml->{socket};
}


# process the config
my %files;
my @toml_keys = keys( %{$toml} );
my $int       = 0;
while ( defined( $toml_keys[$int] ) ) {
	my $item = $toml_keys[$int];

	if ( ref( $toml->{$item} ) eq "HASH" ) {
		# add the file in question
		$files{$item} = $toml->{$item};
	}

	$int++;
}

File::Syslogger->run(
	facility => $fac,
	pri      => $pri,
	socket   => $socket,
	files    => \%files,
);

=head1 NAME

filesyslogger - Tails the configured files and sends it to syslog.

=head1 SYNOPSIS

filesyslogger [B<-P> <program>] [B<-p> <priority>] [B<-f> <facility>] [B<-t> <config>] [B<-s> <socket>]

=head1 FLAGS

=head2 -P <program>

The program name to use. If not specified, 'fileSyslogger' is used.

=head2 -p <priority>

The priority to use. If not specified, 'notice' is used.

=head2 -f <facility>

The facility to use. If not specified, 'daemon' is used.

=head2 -s <socket>

The socket to use. If not specified, '/var/run/log' is used.

=head2 -t <config file>

This is the config file to use. If not specified, '/usr/local/etc/filesyslogger.toml' is used.

=head1 CONFIG FILE

The file format used is TOML.



=cut
